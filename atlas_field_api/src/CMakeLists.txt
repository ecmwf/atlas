add_library( atlas_field_api_objects OBJECT field_ranksuff_module.fypp field_factory_module.fypp field_abort_module.F90 field_util_module.F90 )
set_target_properties( atlas_field_api_objects PROPERTIES Fortran_MODULE_DIRECTORY ${CMAKE_BINARY_DIR}/module/atlas_field_api )
target_include_directories( atlas_field_api_objects PUBLIC $<BUILD_INTERFACE:${CMAKE_BINARY_DIR}/module/atlas_field_api>)

target_link_libraries( atlas_field_api_objects PUBLIC atlas_f pluto_f )
if (OpenMP_Fortran_FOUND)
  target_link_libraries( atlas_field_api_objects PUBLIC OpenMP::OpenMP_Fortran )
endif()
if (OpenACC_Fortran_FOUND)
  target_link_libraries( atlas_field_api_objects PUBLIC OpenACC::OpenACC_Fortran )
endif()
fckit_target_preprocess_fypp( atlas_field_api_objects )

foreach( prec sp dp )
  set( atlas_field_api atlas_field_api_${prec} )
  add_library( ${atlas_field_api} field_module.F90)
  target_link_libraries( ${atlas_field_api} PUBLIC atlas_field_api_objects )
  if( prec STREQUAL "sp" )
      target_compile_definitions( ${atlas_field_api} PRIVATE SINGLE_PRECISION )
  endif()
  set_target_properties( ${atlas_field_api} PROPERTIES Fortran_MODULE_DIRECTORY ${CMAKE_BINARY_DIR}/module/${atlas_field_api} )
  target_include_directories( ${atlas_field_api} PUBLIC $<BUILD_INTERFACE:${CMAKE_BINARY_DIR}/module/${atlas_field_api}>)
endforeach()
